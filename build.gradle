plugins {
  id "idea"
  id "com.github.mxenabled.coppuccino" version "5.+" apply false
  id "com.github.mxenabled.vogue" version "2.+"
  id "io.freefair.lombok" version "8.+" apply false
  id "io.github.gradle-nexus.publish-plugin" version "1.1.+"
  id "com.netflix.nebula.maven-resolved-dependencies" version "21.2.0" apply false
}

version "20.1.0" // x-release-please-version

def platformProject = "platform"
def publishedProjects = [
  platformProject,
  "realtime",
  "mdx-gateways",
  "mdx-models",
  "mdx-web"
]

allprojects {
  if (it.name != platformProject) {
    apply plugin: "java"
  }

  group "com.mx.path-mdx-model"
  description "Path MDX Model"
  version rootProject.version
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  repositories {
    mavenCentral()
    mavenLocal()
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
      //Uncontrolled Recursion [High Severity][https://security.snyk.io/vuln/SNYK-JAVA-ORGAPACHECOMMONS-10734078] in org.apache.commons:commons-lang3@3.17.0
      //    introduced by net.sourceforge.pmd:pmd-java@7.16.0 > org.apache.commons:commons-lang3@3.17.0 and 7 other path(s)
      //  This issue was fixed in versions: 3.18.0
      if (details.requested.group == "org.apache.commons" && details.requested.name == "commons-lang3") {
        details.useVersion "3.18.0"
      }
      //Improper Validation of Certificate with Host Mismatch [Medium Severity][https://security.snyk.io/vuln/SNYK-JAVA-ORGAPACHELOGGINGLOG4J-14532782] in org.apache.logging.log4j:log4j-core@2.24.3
      //    introduced by com.github.spotbugs:spotbugs@4.9.8 > org.apache.logging.log4j:log4j-core@2.24.3
      //  This issue was fixed in versions: 2.25.3
      else if (details.requested.group == "org.apache.logging.log4j" && details.requested.name == "log4j-core") {
        details.useVersion "2.25.3"
      }
      //XML External Entity (XXE) Injection [Medium Severity][https://security.snyk.io/vuln/SNYK-JAVA-ORGASSERTJ-15102413] in org.assertj:assertj-core@3.27.6
      //    introduced by com.mx.path-core:testing@5.1.0 > org.assertj:assertj-core@3.27.6 and 1 other path(s)
      //  This issue was fixed in versions: 3.27.7
      else if (details.requested.group == "org.assertj" && details.requested.name == "assertj-core") {
        details.useVersion "3.27.7"
      }
      //Information Exposure [Low Severity][https://security.snyk.io/vuln/SNYK-JAVA-COMMONSCODEC-561518] in commons-codec:commons-codec@1.11
      //    introduced by io.honeybadger:honeybadger-java@2.1.2 > org.apache.httpcomponents:fluent-hc@4.5.14 > org.apache.httpcomponents:httpclient@4.5.14 > commons-codec:commons-codec@1.11
      //  This issue was fixed in versions: 1.14
      else if (details.requested.group == "commons-codec" && details.requested.name == "commons-codec") {
        details.useVersion "1.14"
      }
    }
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  ext {
    pathSDKVersion = "[6.0,7.0)"
    springVersion = "[3.5,4.0)"
  }
}

subprojects {
  if (it.name != platformProject) {
    apply plugin: "java-library"
    apply plugin: "groovy"
    apply plugin: "com.github.mxenabled.coppuccino"
    apply plugin: "com.github.mxenabled.vogue"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.netflix.nebula.maven-resolved-dependencies"

    dependencies {
      api platform("com.mx.path-core:platform:${project.ext.pathSDKVersion}")

      testImplementation "org.mockito:mockito-inline:[5.0,6.0)"
      testImplementation "org.spockframework:spock-core:2.4-M6-groovy-3.0"
    }

    test { useJUnitPlatform() }

    compileJava { options.compilerArgs << "-parameters" }

    javadoc {
      classpath = configurations.compileClasspath
      options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
      }
    }

    coppuccino {
      rootDir = "${projectDir}/"
      dependencies {
        excludePreReleaseVersions = true
      }
    }

    vogue {
      dependencyUpdatesOutputDir = "${projectDir}/build/dependencyUpdates"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
      classifier = "sources"
      from sourceSets.main.allSource
    }

    task packageJavadoc(type: Jar) {
      classifier = "javadoc"
      from javadoc
    }

    artifacts {
      archives sourcesJar
      archives jar
      archives packageJavadoc
    }

    if (publishedProjects.contains(it.name)) {
      apply plugin: "maven-publish"
      apply plugin: "signing"

      publishing {
        publications {
          maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact packageJavadoc

            pom {
              groupId = project.group
              artifactId = project.name
              name = project.name
              description = project.description
              url = "https://github.com/mxenabled/path-mdx-model"

              developers {
                developer {
                  name = "MX"
                  email = "path@mx.com"
                  organization = "MX Technologies Inc."
                  url = "http://www.mx.com"
                }
              }

              licenses {
                license {
                  name = "Proprietary"
                  url = "https://github.com/mxenabled/path-mdx-model/blob/master/LICENSE"
                  distribution = "repo"
                }
              }

              scm {
                connection = "scm:git:git@github.com:mxenabled/path-mdx-model.git"
                url = "https://github.com/mxenabled/path-mdx-model/tree/master"
              }
            }
          }
        }
      }

      signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingKeyPassword")
        if (signingKey != null && signingKey != "") {
          useInMemoryPgpKeys(signingKey, signingPassword)
          sign publishing.publications.maven
          logger.lifecycle("Configuring signing for ${project.name}")
        } else {
          logger.lifecycle("Skipping artifact signing for ${project.name} - missing signing key")
        }
      }
    }
  }
}

nexusPublishing {
  def ossrhUserName = findProperty("ossrhUserName")
  def ossrhToken = findProperty("ossrhToken")

  repositories {
    central {
      nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
      snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
      username = ossrhUserName
      password = ossrhToken
    }
  }
}

task spotlessApply {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def spotlessApplyTask = it.tasks.findByName("spotlessApply")
        dependsOn(spotlessApplyTask)
      }
    }
  }
}

task subdependencies {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def dependenciesTask = it.tasks.findByName("dependencies")
        dependsOn(dependenciesTask)
      }
    }
  }
}

project.tasks.getByPath("dependencies").finalizedBy("subdependencies")
