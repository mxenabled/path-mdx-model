plugins {
  id "idea"
  id "com.github.mxenabled.coppuccino" version "3.+" apply false
  id "com.github.mxenabled.vogue" version "1.+"
  id "io.freefair.lombok" version "8.0.+" apply false
  id "io.github.gradle-nexus.publish-plugin" version "1.1.+"
}

version "4.6.0" // x-release-please-version

def platformProject = "platform"
def publishedProjects = [
  platformProject,
  "realtime",
  "mdx-gateways",
  "mdx-models",
  "mdx-web"
]

allprojects {
  if (it.name != platformProject) {
    apply plugin: "java"
  }

  group "com.mx.path-mdx-model"
  description "Path MDX Model"
  version rootProject.version
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    mavenLocal()
    // maven {
    //   url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    //   mavenContent {
    //     snapshotsOnly()
    //   }
    // }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  ext {
    pathSDKVersion = "[3.5,4.0)"
    springVersion = "[2.7.0,2.8.0)!!2.7.5"
  }

}

subprojects {
  if (it.name != platformProject) {
    apply plugin: "java-library"
    apply plugin: "groovy"
    apply plugin: "com.github.mxenabled.coppuccino"
    apply plugin: "com.github.mxenabled.vogue"
    apply plugin: "io.freefair.lombok"

    dependencies {
      api platform("com.mx.path-core:platform:${project.ext.pathSDKVersion}")

      testImplementation "org.mockito:mockito-inline:[4.0,5.0)"
      testImplementation "org.spockframework:spock-core:[2.0,3.0)"
      testImplementation "org.junit.jupiter:junit-jupiter-api:[5.9.0,5.10.0)"
    }

    test { useJUnitPlatform() }

    compileJava { options.compilerArgs << "-parameters" }

    sourceSets {
      test {
        groovy { srcDirs "src/test/groovy" }
        java { srcDirs "src/test/java" }
      }
    }

    javadoc {
      classpath = configurations.compileClasspath
      options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
      }
    }

    coppuccino {
      rootDir = "${projectDir}/"
      dependencies {
        excludePreReleaseVersions = true
      }
    }

    vogue {
      dependencyUpdatesOutputDir = "${projectDir}/build/dependencyUpdates"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
      classifier = "sources"
      from sourceSets.main.allSource
    }

    task packageJavadoc(type: Jar) {
      classifier = 'javadoc'
      from javadoc
    }

    artifacts {
      archives sourcesJar
      archives jar
      archives packageJavadoc
    }

    if (publishedProjects.contains(it.name)) {
      apply plugin: "maven-publish"
      apply plugin: "signing"

      publishing {
        publications {
          maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact packageJavadoc

            pom {
              groupId = project.group
              artifactId = project.name
              name = project.name
              description = project.description
              url = "https://github.com/mxenabled/path-mdx-model"

              developers {
                developer {
                  name = "MX"
                  email = "path@mx.com"
                  organization = "MX Technologies Inc."
                  url = "http://www.mx.com"
                }
              }

              licenses {
                  license {
                      name = "Proprietary"
                      url = "https://github.com/mxenabled/path-mdx-model/blob/master/LICENSE"
                      distribution = "repo"
                  }
              }

              scm {
                connection = "scm:git:git@github.com:mxenabled/path-mdx-model.git"
                url = "https://github.com/mxenabled/path-mdx-model/tree/master"
              }
            }
          }
        }
      }

      signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingKeyPassword")
        if (signingKey != null && signingKey != "") {
          useInMemoryPgpKeys(signingKey, signingPassword)
          sign publishing.publications.maven
          logger.lifecycle("Configuring signing for ${project.name}")
        } else {
          logger.lifecycle("Skipping artifact signing for ${project.name} - missing signing key")
        }
      }
    }
  }
}

nexusPublishing {
  def ossrhUserName = findProperty("ossrhUserName")
  def ossrhToken = findProperty("ossrhToken")

  repositories {
    central {
      nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
      snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
      username = ossrhUserName
      password = ossrhToken
    }
  }
}

task spotlessApply {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def spotlessApplyTask = it.tasks.findByName("spotlessApply")
        dependsOn(spotlessApplyTask)
      }
    }
  }
}

task subdependencies {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def dependenciesTask = it.tasks.findByName("dependencies")
        dependsOn(dependenciesTask)
      }
    }
  }
}

project.tasks.getByPath("dependencies").finalizedBy("subdependencies")
