plugins {
  id "com.github.mxenabled.coppuccino" version "6.+" apply false
  id "com.github.mxenabled.vogue" version "3.+"
  id "idea"
  id "io.freefair.lombok" version "8.+" apply false
  id "io.github.gradle-nexus.publish-plugin" version "1.1.+"
  id "com.netflix.nebula.maven-resolved-dependencies" version "21.2.0" apply false
}

version "20.2.3" // x-release-please-version

def platformProject = "platform"
def publishedProjects = [
  platformProject,
  "realtime",
  "mdx-gateways",
  "mdx-models",
  "mdx-web"
]

subprojects {
  group "com.mx.path-mdx-model"
  description "Path MDX Model"
  version rootProject.version

  ext {
    pathCoreVersion = "[6.0,7.0)"
    springVersion = "3.5.8"
  }

  if (it.name == platformProject) {
    apply plugin: "java-platform"
  } else {
    apply plugin: "com.github.mxenabled.coppuccino"
    apply plugin: "com.github.mxenabled.vogue"
    apply plugin: "groovy"
    apply plugin: "java-library"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.netflix.nebula.maven-resolved-dependencies"

    java {
      toolchain {
        languageVersion = JavaLanguageVersion.of(17)
      }
      withSourcesJar()
      withJavadocJar()
    }

    coppuccino {
      rootDir = "${projectDir}/"
      dependencies {
        excludePreReleaseVersions = true
      }
    }

    vogue {
      dependencyUpdatesOutputDir = "${projectDir}/build/dependencyUpdates"
    }

    repositories {
      mavenCentral()
      mavenLocal()
    }

    dependencies {
      api platform("com.mx.path-core:platform:${project.ext.pathCoreVersion}")

      testImplementation "com.mx.path-core:testing"
    }

    test { useJUnitPlatform() }

    compileJava { options.compilerArgs << "-parameters" }

    javadoc {
      classpath = configurations.compileClasspath
      options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
      }
    }
  }

  if (publishedProjects.contains(it.name)) {
    apply plugin: "maven-publish"
    apply plugin: "signing"

    publishing {
      publications {
        maven(MavenPublication) {
          from(project.name == platformProject ? components.javaPlatform : components.java)

          pom {
            groupId = project.group
            artifactId = project.name
            name = project.name
            description = project.description
            url = "https://github.com/mxenabled/path-mdx-model"

            developers {
              developer {
                name = "MX"
                email = "path@mx.com"
                organization = "MX Technologies Inc."
                url = "http://www.mx.com"
              }
            }

            licenses {
              license {
                name = "Proprietary"
                url = "https://github.com/mxenabled/path-mdx-model/blob/master/LICENSE"
                distribution = "repo"
              }
            }

            scm {
              connection = "scm:git:git@github.com:mxenabled/path-mdx-model.git"
              url = "https://github.com/mxenabled/path-mdx-model/tree/master"
            }
          }
        }
      }
    }

    signing {
      def signingKey = findProperty("signingKey")
      def signingPassword = findProperty("signingKeyPassword")
      if (signingKey != null && signingKey != "") {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
        logger.lifecycle("Configuring signing for ${project.name}")
      } else {
        logger.lifecycle("Skipping artifact signing for ${project.name} - missing signing key")
      }
    }
  }
}

nexusPublishing {
  def ossrhUserName = findProperty("ossrhUserName")
  def ossrhToken = findProperty("ossrhToken")

  repositories {
    central {
      nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
      snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
      username = ossrhUserName
      password = ossrhToken
    }
  }
}

task spotlessApply {
  dependsOn subprojects.findAll { it.name != platformProject }.collect { "${it.path}:spotlessApply" }
}

task subdependencies {
  dependsOn subprojects.findAll { it.name != platformProject }.collect { "${it.path}:dependencies" }
}

project.tasks.getByPath("dependencies").finalizedBy("subdependencies")

wrapper {
  gradleVersion = "7.6.4"
  distributionType = Wrapper.DistributionType.ALL
}
